tmpl start_check cond fail_msg
  \match(State, Check, 1) unless $cond run $spset Check State 0
  \match(State, Check, 1) unless $cond run title @p actionbar {"text":$fail_msg,"color":"red"}
end

func start
  call rocket/recalculate
  $spset Check State 1
  \start_check(score FuelUsage Rocket <= Fuel Rocket, "Not enough fuel.")
  \start_check(score Mission State matches $mission_moon.., "Earth is selected.")
  \start_check(score OnMission State matches 0, "A mission is already in progress.")
  \match(State, Check, 1) run function gcv:mission/process_start
end

const
  moon_mission = \match(State, Mission, $mission_moon) in gcv:moon
  mars_mission = \match(State, Mission, $mission_mars) in gcv:mars

  players_earth = @a[$gamers,nbt={Dimension:"minecraft:overworld"}]
  players_space = @a[$gamers,nbt=!{Dimension:"minecraft:overworld"}]
  players_moon  = @a[$gamers,nbt={Dimension:"gcv:moon"}]
end

func process_start
  $log "mission: Starting"

  $spset OnMission State 1

  $spopr Fuel Rocket -= FuelUsage Rocket

  $spset Earned State 500
  $spopr BaseReward State = Mission State
  $spadd BaseReward State 1
  $spopr BaseReward State *= RewardMul State

  call main/gather_gamers
  gamemode survival @a[$gamers]

  $moon_mission run tp @a 0 500 0
  $mars_mission run tp @a 0 500 0

  # Important Note:
  # In order for persistent spawns to work, the chunk they were placed in *must*
  # be /forceloaded
  $moon_mission run tag @e[tag=PersistentSpawn] add Spawnpoint
  $mars_mission run tag @e[tag=PersistentSpawn] add Spawnpoint

  $moon_mission unless entity @e[tag=Spawnpoint] run summon marker 0 500 0 {Tags:["TempSpawn","Spawnpoint"]}
  $mars_mission unless entity @e[tag=Spawnpoint] run summon marker 0 500 0 {Tags:["TempSpawn","Spawnpoint"]}

  spreadplayers 0 0 16 16 false @e[tag=TempSpawn]
  $at @e[tag=Spawnpoint] run spawnpoint @a ~ ~ ~ 0
  $at @e[tag=Spawnpoint] run tp @a ~ 200 ~ 0 89
  effect give @a slow_falling 10 0 true
  tag @a add Falling

  tag @e[tag=Spawnpoint] remove Spawnpoint
  kill @e[tag=TempSpawn]

  schedule function gcv:mission/announce 20t

  schedule function gcv:mission/tick 5t
end

tmpl ensure_gamer_item slot_no slot_name id name
  $at @a[$gamers] unless data entity @p {Inventory:[{Slot:$slot_no,id:"minecraft:$id",tag:{GCV:{Invalidate:1b}}}]} run clear @a[$gamers] $id{GCV:{Invalidate:1b}}
  $at @a[$gamers] unless data entity @p {Inventory:[{Slot:$slot_no,id:"minecraft:$id",tag:{GCV:{Invalidate:1b}}}]} run item replace entity @p $slot_name with $id{display:{Name:'{"text":"$name","italic":false}'},GCV:{Invalidate:1b},Unbreakable:1b}
end

func tick
  call gather_gamers

  gamemode spectator @a[$gamers,scores={Deaths=1..}]
  $spset @a[$gamers] Deaths 0
  $unless entity @a[scores={HasDied=0}] run function gcv:mission/end

  effect clear @a[tag=Falling,nbt={OnGround:1b}] slow_falling
  tag @a[tag=Falling,nbt={OnGround:1b}] remove Falling
  $at @a[tag=Falling] run tp @p ~ ~ ~ 0 89
  $at @a[tag=Falling] run particle minecraft:smoke ~ ~ ~ 0 -2 0 0.1 25 normal @p
  $at @a[tag=Falling] run particle minecraft:campfire_cosy_smoke ~ ~2 ~ 0 -10 0 0.01 25 normal @p
  effect give @a[tag=Falling] slow_falling 5 0 true

  \ensure_gamer_item(103b, armor.head, glass, Astronaut Helmet)
  \ensure_gamer_item(102b, armor.chest, iron_chestplate, Astronaut Suit Top)
  \ensure_gamer_item(101b, armor.legs, iron_leggings, Astronaut Suit Bottom)
  \ensure_gamer_item(100b, armor.feet, iron_boots, Astronaut Boots)
  kill @e[type=item,nbt={Item:{tag:{GCV:{Invalidate:1b}}}}]

  $at $players_space run item replace entity @e[type=zombie,distance=..64,limit=1,nbt=!{ArmorItems:[{},{},{},{id:"minecraft:glass",tag:{GCV:{Invalidate:1b}}}]}] armor.head with glass{GCV:{Invalidate:1b}}
  $at $players_space run item replace entity @e[type=skeleton,distance=..64,limit=1,nbt=!{ArmorItems:[{},{},{},{id:"minecraft:glass",tag:{GCV:{Invalidate:1b}}}]}] armor.head with glass{GCV:{Invalidate:1b}}

  title @a[$gamers,nbt=!{Dimension:"minecraft:overworld"}] actionbar [{"score":{"name":"*","objective":"Oxy"},"color":"$clr_oxy"},{"text":" $txt_oxy"}]

  \match(State, OnMission, 1) run schedule function gcv:mission/tick 5t
end

func end
  $log "mission: Ending"

  call main/gather_gamers

  schedule clear gcv:mission/tick

  $spset OnMission State 0
  $spset @a HasDied 0

  $spopr Fuel Rocket -= FuelUsage Rocket

  $spopr Earned State += BaseReward State
  $spopr @a[$gamers] Balance += Earned State

  tellraw @a [{"text":"---- ","color":"dark_gray"},{"text":"Mission summary","color":"yellow","bold":true},{"text":" ----","color":"dark_gray"}]
  tellraw @a [{"text":"Earnings: ","color":"green"},{"score":{"name":"Earned","objective":"State"},"color":"white"},{"text":"$$","color":"dark_green"}]
  tellraw @a [{"text":"Your balance is now: ","color":"aqua"},{"score":{"name":"*","objective":"Balance"},"color":"white"},{"text":"$$","color":"dark_green"}]
  tellraw @a [{"text":"Leftover fuel: ","color":"red"},{"score":{"name":"Fuel","objective":"Rocket"},"color":"yellow"},{"text":"L","color":"yellow"},{"text":"/","color":"gray"},{"score":{"name":"FuelMax","objective":"Rocket"},"color":"white"},{"text":"L","color":"white"}]
  tellraw @a [{"text":"-------------------------","color":"dark_gray","bold":false}]

  gamemode adventure @a[$gamers]
  effect give @a[$gamers] saturation 30 9
  item replace entity @a[$gamers] armor.head with air
  item replace entity @a[$gamers] armor.chest with air
  item replace entity @a[$gamers] armor.legs with air
  item replace entity @a[$gamers] armor.feet with air
  $in minecraft:overworld run tp @a 0 100 0 0 0
  $in minecraft:overworld run spawnpoint @a 0 100 0 0
end

tmpl announce no name distance
  \match(State, Mission, $no) run title @a subtitle {"text":"~$distance AU away","color":"gold"}
  \match(State, Mission, $no) run title @a title {"text":"$name","color":"green"}
end

func announce
  \announce($mission_moon, The Moon, 0.0026)
  \announce($mission_mars, Mars, 0.3646)
end

const
  MissionTitle    = @e[tag=MissionTitle,limit=1]
  MissionDistance = @e[tag=MissionDistance,limit=1]

  mission_earth = 0
  mission_moon  = 1
  mission_mars  = 2
  mission_last  = $mission_mars
  mission_max   = 3

  head_earth = {SkullOwner:{Id:[I;-475097093,-1232581123,-1897520009,-1863176499],Properties:{textures:[{Value:"eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvMTI4OWQ1YjE3ODYyNmVhMjNkMGIwYzNkMmRmNWMwODVlODM3NTA1NmJmNjg1YjVlZDViYjQ3N2ZlODQ3MmQ5NCJ9fX0="}]}}}
  head_moon  = {SkullOwner:{Id:[I;863215294,-1780921430,-1773460256,1982824210],Properties:{textures:[{Value:"eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvYzAwYTFhN2JiMDdmZGI0ZTZhODZlMzQxODE2ZTg4NDNkZGFmN2NmMzcxM2EzNjY2ZDc0YjcyZjk4NjE5ZjA2MyJ9fX0="}]}}}
  head_mars  = {SkullOwner:{Id:[I;-1927913583,-704033596,-1213014585,-386995908],Properties:{textures:[{Value:"eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvNzc3ZDYxNmJjNDRhYzliMzczMGZlZDQ3ZjI5YTM3OGY4OGExNjcyOGM2NzA0OGMxYTM4N2QyMjllMWNiYSJ9fX0="}]}}}
end

func next
  $spadd Mission State 1
  \match(State, Mission, $mission_max) run $spset Mission State 0
  call update_title
end

func prev
  $sprem Mission State 1
  \match(State, Mission, -1) run $spset Mission State $mission_last
  call update_title
end

tmpl mission_title no name color flavor head
  \match(State, Mission, $no) run data modify entity $MissionTitle CustomName set value '{"text":"$name","color":"$color"}'
  \match(State, Mission, $no) run data modify entity $MissionDistance CustomName set value '{"text":"$flavor","color":"green"}'
  \match(State, Mission, $no) run data modify entity @e[tag=MissionTitle,limit=1] ArmorItems[3] set value {id:"minecraft:player_head",Count:1b,tag:$head}
end

func update_title
  \mission_title($mission_earth, Earth, green, You are here!, $head_earth)
  \mission_title($mission_moon, The Moon, gray, 450L fuel required, $head_moon)
  \mission_title($mission_mars, Mars, red, 1350L fuel required, $head_mars)
end
